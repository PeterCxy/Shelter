apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "net.typeblog.shelter"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 18
        versionName "1.7-dev1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // We have community-contributed translations. Do not let them block releases.
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        // We don't need Google App Indexing
        disable 'GoogleAppIndexingWarning'
        // Some dependencies still pull in Fragment 1.2.x
        // Let's just ignore the error for now
        // We don't really hit the broken use-cases for now
        disable 'InvalidFragmentVersionForActivityResult'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.fragment:fragment:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'mobi.upod:time-duration-picker:1.1.3'
    debugImplementation project(path: ':setup-wizard-lib', configuration: 'gingerbreadCompatDebugRuntimeElements')
    releaseImplementation project(path: ':setup-wizard-lib', configuration: 'gingerbreadCompatReleaseRuntimeElements')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.4.0-alpha04'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0-alpha04'
}
